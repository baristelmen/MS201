@Parser    Implicit;
@Author    Baris Telmen;
@Behaviour NonLinearIsotropicViscosity;
@Description{
  Non-Linear isotropic hardening with
  viscosity addition
};

@Brick StandardElasticity;
@Theta 1.;
@Epsilon 1.e-13;
@CompareToNumericalJacobian true;
@JacobianComparisonCriterium 1.e-10;

@Profiling true;

@MaterialProperty stress young;
young.setGlossaryName("YoungModulus");
@MaterialProperty real nu;
nu.setGlossaryName("PoissonRatio");

@MaterialProperty stress R_inf;
@MaterialProperty stress R_0;
@MaterialProperty real b;
@MaterialProperty real K;
@MaterialProperty real m;

@StateVariable strain p;
p.setGlossaryName("EquivalentPlasticStrain");

@LocalVariable bool Fel;

@InitLocalVariables{
  StressStensor scin = computeElasticPrediction();
  const stress seq = sigmaeq(scin);
  const stress Rpel = R_inf + (R_0 - R_inf)*exp(-b*p);
  Fel = seq - Rpel > 0;
}

@Integrator{
  constexpr const real eps = 1.e-12;
  if (Fel)
  {
    const auto mu       =   computeMu(young,nu);
    StressStensor scin  =   sig;
    const stress seq    =   sigmaeq(scin);

    const real p_       =   p + theta*dp;
    const real Rp       =   R_inf + (R_0 - R_inf)*exp(-b*p_);

    const stress F      =   max(seq - Rp, stress(0));
    const real Fexp     =   pow(F/K,m-1)/K;
    const auto iseq     =   1/max(seq,eps*young);

    const Stensor n     =   3*deviator(scin)*iseq/2;
    //const Stensor n   =   1.5 * deviator(sig) / sigmaeq(sig);
    const auto vp       =   Fexp*F;

    feel += dp*n;
    fp   -= vp * dt;

    const Stensor4 Jmn  =   Stensor4::M() - (n^n);
    dfeel_ddp           =   n;
    dfeel_ddeel        +=   2*mu*theta*dp*Jmn*iseq ;
    dfp_ddp             =   1 + theta* Fexp *m *dt*b*(R_inf-Rp);
    dfp_ddeel           =   -2*Fexp*m*dt* 2.*mu*theta*(n|Stensor4::M())/3;
  }
}
